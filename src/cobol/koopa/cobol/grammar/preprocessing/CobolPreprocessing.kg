grammar CobolPreprocessing extends CobolPreprocessingBase.

## #############################################################################
## Top level definitions.
## -----------------------------------------------------------------------------

# Preprocessing.
def preprocessing =
	( (--> preprocessingDirective) preprocessingDirective )*
end

def preprocessingDirective =
	( copyStatement
	| replaceStatement
	)
end


## #############################################################################
## COPY statement.
## -----------------------------------------------------------------------------

def copyStatement =
    COPY textName [ (OF | IN) libraryName ]
    [SUPPRESS [PRINTING]]
    [copyReplacingPhrase]
    .
end

def copyReplacingPhrase =
	REPLACING
	(copyReplacementInstruction)+
end

def copyReplacementInstruction =
	[leading | trailing] copyOperandName BY copyOperandName
end

def leading  =  LEADING   end
def trailing =  TRAILING  end

def copyOperandName =
	(pseudoLiteral | literal | cobolWord)
end


## #############################################################################
## REPLACE statement.
## -----------------------------------------------------------------------------

def replaceStatement =
    ( replaceStatement_format1
    | replaceStatement_format2
    )
end

private def replaceStatement_format1 =
    REPLACE [ALSO]
    ( pseudoLiteral BY pseudoLiteral
    | (LEADING | TRAILING) pseudoLiteral BY pseudoLiteral
    )+
    .
end

private def replaceStatement_format2 =
    REPLACE [LAST] OFF .
end


## #############################################################################
## Lower level stuff.
## -----------------------------------------------------------------------------

def textName =
    ( cobolWord
    | alphanumericLiteral
    )
end

def libraryName =
    ( cobolWord
    | alphanumericLiteral
    )
end

def literal =
    ( numeric
    | alphanumericLiteral
    )
end

def numeric =
    ( integerLiteral
    | decimal
    | hexadecimal
    )
end

# -----------------------------------------------------------------------------
# Decimals
#
# Whether we're working on a decimal is something which can only be decided at
# parsing time. But it does require some lexer info.
#
# TODO Allow ",2"; which is still tricky.
def decimal =
    ( ('+' | '-') %noskip unsigned_decimal
    | unsigned_decimal
    )
end

private def unsigned_decimal =
    ( intgr %noskip ( (',' | '.') uintgr )
    | '.' %noskip uintgr
    )
end

private def intgr =
    @INTEGER_LITERAL _
end

private def uintgr =
    @UNSIGNED @INTEGER_LITERAL _
end
